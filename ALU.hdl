// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.

    
    Mux16(a=x, b=false, sel=zx, out=x1);
    
    Not16(in=x1, out=xneg);
    Mux16(a=x1, b=xneg, sel=nx, out=xfin);

    Mux16(a=y, b=false, sel=zy, out=y1);
    Not16(in=y1, out=yneg);
    Mux16(a=y1, b=yneg, sel=ny, out=yfin);
    
    And16(a=xfin, b=yfin, out=anded);
    Add16(a=xfin, b=yfin, out=added);

    Mux16(a=anded, b=added, sel=f, out=out1);
    
    Not16(in=out1,out=outnot);
    Mux16(a=out1, b=outnot, sel=no, out=fin);

    Or16Way(a=fin,out=or3);
    Not(in=or3, out=zr);

    And16(a[15]=true,b=fin,out[15]=ng,out[0..14]=na);

    And16(a=true, b=fin, out=out);
}

